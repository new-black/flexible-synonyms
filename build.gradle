version = '6.6.2'

buildscript {
    ext.kotlin_version = '1.3.0'
    ext.elasticsearch_version = '6.6.2'
    ext.log4j_version = '2.9.1'

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'kotlin'
apply plugin: 'idea'

repositories {
    mavenCentral()
}

configurations {
    localDeps
}

dependencies {
    localDeps group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib', version: kotlin_version
    localDeps group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.6'

    implementation configurations.localDeps

    compileOnly "org.elasticsearch:elasticsearch:$elasticsearch_version"
    compileOnly "org.apache.logging.log4j:log4j-core:$log4j_version"
    compileOnly "org.apache.logging.log4j:log4j-api:$log4j_version"

    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.3.1'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.23.0'
    testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    testCompile "org.elasticsearch.test:framework:$elasticsearch_version"
}

task copyForDist(type: Copy) {
    dependsOn build

    mkdir "$buildDir/elasticsearch"

    from "$libsDir"
    include "*.jar"
    into "$buildDir/elasticsearch"

    from configurations.localDeps
    into "$buildDir/elasticsearch"

    from "$buildDir/resources/main"
    include '*'
    into "$buildDir/elasticsearch"
}

task distZip(type: Zip) {
    dependsOn copyForDist

    archivesBaseName = rootProject.name
    destinationDir = file("$buildDir/dist")

    from "$buildDir/elasticsearch"
    into "elasticsearch"
}